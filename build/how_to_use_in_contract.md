# How to access data in POD using contracts?

POD’s on-chain database is designed for smart contracts to quickly access data. Now let's go through an example to understand how to use data in POD via a contract.

Suppose you are a user reputation score provider. You generate user reputation data based on their on-chain behaviours, like their addresses’ transaction records in a DeFi project. Your customers obtain such data from you so they can evaluate and categorise their users.

## Create TagClass

In POD, all data belongs to a certain category which is indicated in the form of a [TagClass](https://poddbio.github.io/Docs/#/learn/Technology#tagclass).

Therefore, to store user reputation data, we first create a TagClass with the name of `ReputationTagClass`. (You can create a TagClass using POD dApp or SDK. In order to introduce how to use POD with a contract, here we illustrate how to create the `ReputationTagClass` directly using a contract). First we create a contract, then create the `ReputationTagClass` in the constructor of this contract, and record the TagClassId of the `ReputationTagClass`.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;

import "./librarys/OpenZeppelin/Ownable.sol";
import "./librarys/PodDB/IPodCore.sol";
import "./librarys/PodDB/ITag.sol";
import "./librarys/PodDB/ITagClass.sol";
import "./librarys/PodDB/Helper.sol";
import "./librarys/PodDB/WriteBuffer.sol";

contract Reputation is Ownable {
    using Helper for *;
    using WriteBuffer for *;
    using ReadBuffer for *;

    ITag private _Tag;
    ITagClass private _TagClass;
    bytes18 public ReputationTagClassId;

    constructor(address tagAddress, address tagClassAddress) Ownable() {
        _Tag = ITag(tagAddress);
        _TagClass = ITagClass(tagClassAddress);

        //create reputation tagClass;
        Helper.TagClassFieldBuilder memory builder;
        builder.init().put("Score", IPodCore.TagFieldType.Uint16, false);
        
        ITagClass.NewValueTagClassParams memory params;
        params.TagName = "reputation";
        params.Desc = "Reputation for user";
        params.FieldNames = builder.getFieldNames();
        params.FieldTypes = builder.getFieldTypes();

        ReputationTagClassId = _TagClass.newValueTagClass(params);
    }
}
```

The `IPOD.sol`, `Helper.sol`, and `WriteBuffer.sol` used in the contract can be found in the [POD repository](https://github.com/PodDBio/poddb-evm.git).

Please be aware that POD may be upgraded in the future. POD is backward compatible. Later versions can parse data generated by earlier versions, but it’s not true the other way around. Hence, to ensure the contract can use data generated by new versions, you should allow the update of the POD contract address by adding the following code:

```solidity
function setPodDB(address tagAddress, address tagClassAddress)
public
onlyOwner
{
    _Tag = ITag(tagAddress);
    _TagClass = ITagClass(tagClassAddress);
}
```

### Define TagClass Fields

TagClass fields specify what data are stored in this TagClass, and how to parse the data.

You need to define the name and data type of the fields.

When defining the data type, use the hexadecimal code for a type illustrated in the POD [type enumeration](https://poddbio.github.io/Docs/#/learn/Technology#fieldtype).

POD's Helper tool library provides a TagClass `FieldBuilder` struct to help POD users define TagClass fields. In `ReputationTagClass`, we only define a field named `Score` supporting an Uint16 value to store a user's reputation score.

### Configure Agent

For now, we don’t configure any agent for `ReputationTagClass`. It’s possible to do this later if needed.
Please read more about the Agent [here](https://poddbio.github.io/Docs/#/learn/Technology#tagagent).

## Write Data

The basic data unit in TagClass is Tag, and the action of writing data is called `SetTag`. If you write reputation data for the same user more than once, the new data overwrites the old one.

As a reputation data provider, you calculate users’ reparation scores, which is an off-chain process. Then we can save reputation scores in POD through the `setReputation` contract function. Use the following code for `setReputation`:

```solidity
function setReputation(uint16 reputation, address user) public onlyOwner {
    IPodCore.TagObject memory object = IPodCore.TagObject(
        IPodCore.ObjectType.Address,
        bytes20(user),
        0
    );
    WriteBuffer.buffer memory wBuf;
    bytes memory data = wBuf.init(2).writeUint16(reputation).getBytes();
    uint32 expiredTime = 0; //never expired;
    _Tag.setTag(ReputationTagClassId, object, data, expiredTime);
}
```

All data in Tags should be associated with an on-chain entity, which is represented by a [TagObject](https://poddbio.github.io/Docs/#/learn/Technology#tagobject) . In our example here, the TagObject is a user address, so it can be constructed using `TagObject (msg.sender, 0)`.

The most important thing for`SetTag` is to construct the data in a Tag. The data needs to be serialised for transfer and storage between contracts.

You can use `WriteBuffer` for data serialisation. Please initialise sufficient storage capacity so that you don’t need to worry about adding capacity later.

### Data Expiration Time

When you write data in the form of a Tag, you can specify the expiration time using an Uint32 number to represent seconds. The actual expiration time should take into account the block time when the transaction was sent. Once expired, a Tag cannot be queried.

Since we don’t want the reputation data to expire, we fill the `ExpiredTime` field in the ReputationTag with 0, which means the data never expires.

## Read Data

As a reputation data provider, you usually don’t read your own data in POD. To explain how to read data from POD, we also put this part of the code into the Reputation contract.

There are three functions for reading data from POD: `hasTag`, `getTagData`, and `getTag`.

### hasTag

Calling `hasTag` is used to check whether in a specific TagClass exists a Tag that belongs to a certain TagObject, regardless of what data is stored in the Tag. If no such Tag exists, or the Tag has expired, `false` will be returned.

The `hasTag` is usually used for checking whether a user has certain permissions or rights.


### getTagData

Calling `getTagData` is used to obtain the data stored in a Tag that is related to a certain TagObject from a specific TagClass. If no such Tag exists, or the Tag has expired, an empty byte array will be returned.

Here is the code for getting the reputation score of a specific user:

```solidity
function getReputation(address user) public view returns (uint16 score) {
    IPodCore.TagObject memory object = IPodCore.TagObject(
        IPodCore.ObjectType.Address,
        bytes20(user),
        0
    );
    bytes memory data = _Tag.getTagData(ReputationTagClassId, object);
    if (data.length == 0) {
        return 0;
    }
    ReadBuffer.buffer memory rBuf = ReadBuffer.fromBytes(data);
    score = rBuf.readUint16();
    return score;
}
```

> First check whether the returned data is empty. It’s possible that there’s no reputation score for this user.

Now you need to parse the data stored in the Tag. First you need to know the data type specified in this TagClass, which can be checked using POD SDK or POD dApp. Then each field can be read one by one through the `ReadBuffer` library.

As defined earlier, each field in `ReputationTagClass` carries an Uint16 value, so you read Uint16 values to get the score.

If a field stores an array, you need to read the length of the array with `readLength` first, and then read each element based on the length.

### getTag

By using `getTag` you can obtain more metadata of a Tag than using `getTagData`, such as data expiration time and so on. You can call this function when necessary.

## Delete Data

You can delete unwanted data using the `deleteTag` function. The code is as follows:

```solidity
function deleteReputation(address user) public onlyOwner {
    IPodCore.TagObject memory object = IPodCore.TagObject(
        IPodCore.ObjectType.Address,
        bytes20(user),
        0
    );
    _Tag.deleteTag(ReputationTagClassId, object);
}
```

## Update TagClass

After a TagClass is created, the field types and field names are not editable. The TagClass name and description can be modified using the `UpdateTagClassInfo` function. You can change the Owner, Agent, and TagClassFlags using the UpdateTagClass function.

### Deprecate TagClass

You can deprecate a TagClass using the `UdpateTagClass` Flags. The code is as follows:

```solidity
function deprecatedReputationTagClass() public onlyOwner {
    IPodCore.TagClass memory reputationTagClass = _TagClass.getTagClass(
        ReputationTagClassId
    );
    reputationTagClass.Flags |= 128;
    _TagClass.updateValueTagClass(
        reputationTagClass.ClassId,
        reputationTagClass.Flags,
        reputationTagClass.Agent
    );
}
```

A deprecated TagClass can be reversed to normal the state through `UpdateTagClass`.

Please find the full code in the [poddb-evm-example](https://github.com/PodDBio/poddb-evm-example.git) repository.
